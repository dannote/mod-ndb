#include "../mod_ndb.h"

COMPILER JSON

ap_pool *pool;
apr_table_t *tabl;
char *key, *val;  

  
CHARACTERS
  bslash = '\\' .
  quote = '"' .
  control = '\u0000' .. '\u001f' .  
  char = ANY - control - bslash - quote .
  zero = '0'.
  digit = "123456789" .
  dec = zero + digit .
  hex = dec + "ABCDEF" + "abcdef" .
  escapecode = quote + bslash + "/bfnrt" .


TOKENS 
  string = quote { char | (bslash (escapecode | 'u' hex hex hex hex)) } quote .
  number = ['-'] (zero|digit{dec}) ['.' dec{dec}] [('e'|'E')['+'|'-'] dec{dec}].

IGNORE '\r' + '\t' + '\n' 
  
PRODUCTIONS  
  JSON = object | array . 
  object = '{' [ pair { ',' pair } ] '}' .
  pair = string                       (. key = JSON_string(pool, t);        .)
  ':' value                           (. ap_table_setn(tabl, key, val);   .) .
  array = '[' [ value { ',' value }  ] ']' .
  value = string                      (. val = JSON_string(pool, t);        .)
        | number                      (. val = JSON_string(pool, t);        .)
        | object                      
        | array 
        | "true"                      (. val = JSON_string(pool, t);        .)
        | "false"                     (. val = JSON_string(pool, t);        .)
        | "null"                      (. val = 0;                           .) .    

END JSON .
